"""
    Build rules for buildbarn portal
"""

module(name = "com_github_buildbarn_bb_portal")

http_archive = use_repo_rule("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

bazel_dep(name = "bazel_protos")
local_path_override(
    module_name = "bazel_protos",
    path = "third_party/bazel",
)

bazel_dep(name = "aspect_bazel_lib", version = "2.22.5")
bazel_dep(name = "ape", version = "1.1.0")
bazel_dep(name = "bazel_remote_apis", version = "0.0.0")
bazel_dep(name = "com_github_buildbarn_bb_browser")
bazel_dep(name = "com_github_buildbarn_bb_remote_execution")
bazel_dep(name = "com_github_buildbarn_bb_storage")
bazel_dep(name = "com_github_buildbarn_go_xdr")
bazel_dep(name = "gazelle", version = "0.47.0")
bazel_dep(name = "googleapis", version = "0.0.0-20260109-6145b5ff")
bazel_dep(name = "googletest", version = "1.17.0")
bazel_dep(name = "protobuf", version = "33.4")
bazel_dep(name = "platforms", version = "1.0.0")
bazel_dep(name = "rules_go", version = "0.59.0")
bazel_dep(name = "rules_img", version = "0.3.4")
bazel_dep(name = "rules_jsonnet", version = "0.7.2")
bazel_dep(name = "rules_proto", version = "7.1.0")
bazel_dep(name = "rules_shell", version = "0.6.1")
bazel_dep(name = "toolchains_llvm", version = "1.6.0")
bazel_dep(name = "opentelemetry-proto", version = "1.8.0")

git_override(
    module_name = "bazel_remote_apis",
    commit = "080cf125032eb61b21a53c72c1a09a45145fc152",
    remote = "https://github.com/bazelbuild/remote-apis.git",
)

git_override(
    module_name = "com_github_buildbarn_bb_browser",
    commit = "765b497e89eefc1fa39acb015559ce0c9c490dd7",
    remote = "https://github.com/buildbarn/bb-browser.git",
)

git_override(
    module_name = "com_github_buildbarn_bb_remote_execution",
    commit = "749fb408bd300672b8f13860253d07fdc26c49ff",
    remote = "https://github.com/buildbarn/bb-remote-execution.git",
)

git_override(
    module_name = "com_github_buildbarn_bb_storage",
    commit = "bebcf0861b91da71f9dec8ae8dd5bb2f8a0e1169",
    remote = "https://github.com/buildbarn/bb-storage.git",
)

git_override(
    module_name = "com_github_buildbarn_go_xdr",
    commit = "236788cf9e8948a1e8875c7dd243e2be180cdfd7",
    remote = "https://github.com/buildbarn/go-xdr.git",
)

git_override(
    module_name = "rules_antlr",
    commit = "89a29cca479363a5aee53e203719510bdc6be6ff",
    patches = [
        "//:patches/rules_antlr/antlr-4.10.diff",
        "//:patches/rules_antlr/bzlmod.diff",
    ],
    remote = "https://github.com/marcohu/rules_antlr.git",
)

single_version_override(
    module_name = "gazelle",
    patches = ["//:patches/gazelle/dont-flatten-srcs.diff"],
)

single_version_override(
    module_name = "jsonnet_go",
    patches = ["//:patches/jsonnet_go/bazel-9.diff"],
)

single_version_override(
    module_name = "toolchains_llvm",
    patches = [
        "//:patches/toolchains_llvm/non-root.diff",
        "//:patches/toolchains_llvm/runtime_libs.diff",
    ],
)

go_sdk = use_extension("@rules_go//go:extensions.bzl", "go_sdk")
go_sdk.download(version = "1.25.4")

go_deps = use_extension("@gazelle//:extensions.bzl", "go_deps")
go_deps.gazelle_override(
    build_file_generation = "on",
    path = "github.com/cncf/xds/go",
)
go_deps.module_override(
    patches = [
        "@com_github_buildbarn_bb_remote_execution//:patches/com_github_hanwen_go_fuse_v2/direntrylist-offsets-and-testability.diff",
        "@com_github_buildbarn_bb_remote_execution//:patches/com_github_hanwen_go_fuse_v2/writeback-cache.diff",
        "@com_github_buildbarn_bb_remote_execution//:patches/com_github_hanwen_go_fuse_v2/notify-testability.diff",
    ],
    path = "github.com/hanwen/go-fuse/v2",
)
go_deps.module_override(
    patches = ["@com_github_buildbarn_bb_storage//:patches/org_golang_x_sys/o-search.diff"],
    path = "golang.org/x/sys",
)
go_deps.module_override(
    patches = [
        "@com_github_buildbarn_bb_storage//:patches/org_uber_go_mock/mocks-for-funcs.diff",
    ],
    path = "go.uber.org/mock",
)
go_deps.module_override(
    patches = [
        "//:patches/com_github_bazelbuild_buildtools/buildifier-target-cfg.diff",
    ],
    path = "github.com/bazelbuild/buildtools",
)
go_deps.from_file(go_mod = "//:go.mod")
use_repo(
    go_deps,
    "cc_mvdan_gofumpt",
    "com_github_99designs_gqlgen",
    "com_github_aereal_otelgqlgen",
    "com_github_bazelbuild_bazel_src_main_java_com_google_devtools_build_lib_buildeventstream_proto",
    "com_github_bazelbuild_bazel_src_main_java_com_google_devtools_build_lib_packages_metrics",
    "com_github_bazelbuild_bazel_src_main_protobuf",
    "com_github_bazelbuild_buildtools",
    "com_github_fergusstrange_embedded_postgres",
    "com_github_google_uuid",
    "com_github_gorilla_mux",
    "com_github_hashicorp_go_multierror",
    "com_github_hedwigz_entviz",
    "com_github_improbable_eng_grpc_web",
    "com_github_jackc_pgx_v5",
    "com_github_klauspost_compress",
    "com_github_lib_pq",
    "com_github_machinebox_graphql",
    "com_github_pkg_errors",
    "com_github_prometheus_client_golang",
    "com_github_roaringbitmap_roaring",
    "com_github_rs_cors",
    "com_github_sqlc_dev_pqtype",
    "com_github_stretchr_testify",
    "com_github_uptrace_opentelemetry_go_extra_otelsql",
    "com_github_vektah_gqlparser_v2",
    "io_entgo_contrib",
    "io_entgo_ent",
    "io_opentelemetry_go_otel",
    "io_opentelemetry_go_otel_trace",
    "org_golang_google_genproto",
    "org_golang_google_genproto_googleapis_bytestream",
    "org_golang_google_grpc",
    "org_golang_google_protobuf",
    "org_golang_x_lint",
    "org_golang_x_sync",
    "org_uber_go_mock",
)

llvm = use_extension("@toolchains_llvm//toolchain/extensions:llvm.bzl", "llvm", dev_dependency = True)
llvm.toolchain(
    # TODO: Rename to llvm_toolchain_linux_x86_64 when llvm_toolchain_llvm has become a dev dependency in Buildbarn.
    name = "llvm_toolchain_llvm",
    exec_arch = "x86_64",
    exec_libs_sha256 = "ab7c6cf17f3dff0967332f5d6b3b0dbd26abadfe5c3a171494ceb7f2125e4969",
    exec_libs_strip_prefix = "runtime_libs/",
    # Add any libraries required for the runtime. These are libraries required
    # to run the clang binaries themselves but are otherwise not used in as
    # inputs to the build.
    exec_libs_url = "https://github.com/meroton/llvm_runtime_libraries/releases/download/v20.1.2-alpha/runtime-libs-llvm-20.1.2-linux-x86_64.tar.zst",
    exec_os = "linux",
    # Use any llvm version, independently of the target sysroot. Newer is
    # usually better.
    llvm_version = "20.1.2",
    stdlib = {
        "linux-x86_64": "stdc++",
        "linux-arm64": "stdc++",
    },
)

# TODO: 2026-02-17 Add exec_libs_url and register toolchains.
llvm.toolchain(
    name = "llvm_toolchain_linux_arm64",
    exec_arch = "arm64",
    exec_os = "linux",
    llvm_version = "20.1.2",
)

# Use any sysroot which contains all required link time headers and libraries.
# Typically you target as old as a sysroot as feasible.
http_archive(
    name = "sysroot_linux_x86_64",
    build_file_content = """filegroup(name = "sysroot", srcs = glob(["**/*"]), visibility = ["//visibility:public"])""",
    sha256 = "77f5a2e793607139c8ad9607772e7862b1711430a9a1b2926c16128c3355e987",
    urls = ["https://github.com/scasagrande/toolchains_llvm_sysroot/releases/download/linux-sysroot-4_18-2_28-10_3_0/linux-sysroot-x86_64.tar.zst"],
)

http_archive(
    name = "sysroot_linux_aarch64",
    build_file_content = """filegroup(name = "sysroot", srcs = glob(["**/*"]), visibility = ["//visibility:public"])""",
    sha256 = "8462b3c1bbb7d6e6b5ec48e3b64d59671155b0a522a28e3bcabef67bced3e008",
    urls = ["https://github.com/scasagrande/toolchains_llvm_sysroot/releases/download/linux-sysroot-4_18-2_28-10_3_0/linux-sysroot-aarch64.tar.zst"],
)

# Associate the sysroot with the above toolchain.
llvm.sysroot(
    name = "llvm_toolchain",
    label = "@sysroot_linux_x86_64//:sysroot",
    targets = ["linux-x86_64"],
)
llvm.sysroot(
    name = "llvm_toolchain",
    label = "@sysroot_linux_aarch64//:sysroot",
    targets = ["linux-aarch64"],
)
use_repo(llvm, "llvm_toolchain")

register_toolchains("@llvm_toolchain//:all")

# SQLC tooling

SQLC_VERSION = "1.30.0"

SQLC_PLATFORMS = {
    "darwin_amd64": "eb065ca44f02a9500f8e51cb63594a6bbd2486af04d18c0f81efadf7eadf5e29",
    "darwin_arm64": "ff18793b97715d08dde364446f43082a06da87b7797b9ec79ef2b31aeb0894e5",
    "linux_amd64": "468aecee071bfe55e97fcbcac52ea0208eeca444f67736f3b8f0f3d6a106132e",
    "linux_arm64": "dd9ab43b022ba3b3402054f99d7ae6e5efea33c949e869c3c66b214415e0c82d",
}

[
    http_archive(
        name = "sqlc_" + platform,
        build_file_content = """exports_files(["sqlc"])""",
        sha256 = sha,
        urls = ["https://github.com/sqlc-dev/sqlc/releases/download/v%s/sqlc_%s_%s.tar.gz" % (SQLC_VERSION, SQLC_VERSION, platform)],
    )
    for platform, sha in SQLC_PLATFORMS.items()
]

# Embedded Postgres tooling. Used to run tests sqlc generation, without needing
# to have a Postgres server installed on the system.
EMBEDDED_POSTGRES_VERSION = "18.1.0"

EMBEDDED_POSTGRES_PLATFORMS = {
    "darwin-amd64": {
        "sha": "3b4f16dcf57250d8f9c5e9270a7dfc3921b66d66534d00f8f366e8263e862d11",
        "url_platform_name": "darwin-amd64",
        "tar_platform_name": "darwin-x86_64",
    },
    "darwin-arm64": {
        "sha": "7398c6c19d11594a944dc0ddc79fdbb611164312e1f509a8c86b09724027401e",
        "url_platform_name": "darwin-arm64v8",
        "tar_platform_name": "darwin-arm_64",
    },
    "linux-amd64": {
        "sha": "65d0723e0274ad3ec6230dcbdfe034af6ee096fce15a77b0f9b8be71d2702934",
        "url_platform_name": "linux-amd64",
        "tar_platform_name": "linux-x86_64",
    },
    "linux-arm64": {
        "sha": "ea0f16b754fc0a10d9d8993bbd426c20f8c709b318b28e49b42d9cac1c958b8f",
        "url_platform_name": "linux-arm64v8",
        "tar_platform_name": "linux-arm_64",
    },
}

[
    http_archive(
        name = "embedded_postgres_" + name,
        build_file_content = """
filegroup(
    name = "nested_tar",
    srcs = ["postgres-%s.txz"],
    visibility = ["//visibility:public"],
)
""" % platform["tar_platform_name"],
        sha256 = platform["sha"],
        type = "zip",
        urls = ["https://repo1.maven.org/maven2/io/zonky/test/postgres/embedded-postgres-binaries-%s/%s/embedded-postgres-binaries-%s-%s.jar" % (
            platform["url_platform_name"],
            EMBEDDED_POSTGRES_VERSION,
            platform["url_platform_name"],
            EMBEDDED_POSTGRES_VERSION,
        )],
    )
    for name, platform in EMBEDDED_POSTGRES_PLATFORMS.items()
]
